#Feature +nasalised, +nasal, +voiced
#
#Diacritic ̃  (floating) [+nasalised]
#Diacritic ̃̃  (floating) [+nasal]
#Diacritic ̥  (floating) [-voiced]

## TODO: Run all of this through a unicode canonicalisation program to
##       make sure we’re not using both e.g. ‘y + combining tilde’, and
##       the actual unicode character ‘y with tilde above’.

Symbol d2, b2, bʱ, ȷ̊, ʎ̝, ʑʶ, ɕʶ, sʶ, zʶ, vʶ, βʶ, ʃʶ, ʒʶ, ɮ̃ʶ

Class cn  {ɰ̃, ɥ̃, ʋ̃, ɮ̃, ʎ̝̃, ɮ̃ʶ}
Class n   {m, n, ɲ, ŋ}
Class pk  {p, k, b, g}
Class p   {p, t, k, b, d, g, b2, d2}
Class cpf {@n, @pk, t, d, f, s, ʃ, ʁ, v, z, ʒ, l, j, ɥ, w}
Class cuf {ɰ, ʎ, x, ɕ, ʑ, θ, ð, ɸ, β, ȷ̊, bʱ, sʶ, ɕʶ, zʶ, ʑʶ, vʶ, βʶ, ʀ}
Class c   {@cn, @cpf, @cuf}
Class vn  {ĩ, ỹ, ẽ, ɔ̃, õ, ɛ̃, ã, ɑ̃, ũ, ə̃}
Class vnn {ĩ̃, ỹ̃, ẽ̃, ɔ̃̃, õ̃, ɛ̃̃, ã̃, ɑ̃̃, ũ̃, ə̃̃}
Class vq  {i̥, ẙ, e̥, ɔ̥, o̥, ɛ̥, ḁ, ɑ̥, u̥, ə̥}
Class vo  {i, y, e, ɔ, o, ɛ, a, ɑ, u, ə}
Class v   {@vn, @vnn, @vq, @vo}

Class gnsnasal {@n, @cn, ɥ, ɰ}

Prelim:
  {ɡ, g, w} => ɰ
  Then: {œ, œ̃, ø} => {y, ỹ, ỹ}
  Then: ɔ => o
  Then: uo => vo
  Then: $$ => * / y _ @v
  Then: y => j / _ @v
  Then: $$ => * / @v$1 _ $1
  Then: @v$1 $1 => $1
  Then: {lj, lɥ} => ʎ
  Then: j => ɥ
  Then: ɰ => ɥ / _ i
  Then: ʁʁ => ʀ
  Then: ʁ => ɰ
  Then: @c => * / $ _ @c
  Then: @c => * / @c _ $
  Then: {e, ẽ, ɛ, ɛ̃} => * / $ _ @c
  Then: k => x
  Then: xɥ => ɕ
  Then: {sɰ, ʃɰ, zɰ, ʒɰ, vɰ} => {sʶ, ʃʶ, zʶ, ʒʶ, vʶ}
  Then: {ʃ, ʃʶ, ʒ, ʒʶ} => {ɕ, ɕʶ, ʑ, ʑʶ}
  Then: nt => nθ
  Then: t => d2
  Then: p => b2
  Then: {f, v, vʶ} => {ɸ, β, βʶ}

GNS:
  $$ => * / @vn _ l
  Then: @vn l => ɰ̃
  Then:
    @vo => @vnn / @gnsnasal _ @n $
    @vo => @vn / _ @gnsnasal
    @vn => @vnn / _ @gnsnasal
    @vo => @vn / @gnsnasal _
    @vn => @vnn / @gnsnasal _
  Then: {ə̃, ə̃̃, ã, ã̃, õ, õ̃} => {ɛ̃, ɛ̃̃, ɑ̃, ɑ̃̃, ɔ̃, ɔ̃̃}
  Then: {@n, @cn} => * / @v _ $
  Then: {ɲ, ŋ} => n
  Then: {@vo, @vn} => * / @n _ @n
  Then: {m, l, ʎ} => {ʋ̃, ɮ̃, ʎ̝̃}
  Then: {ɮ̃ɰ, ɰɮ̃} => ɮ̃ʶ

Lenition:
  {x, s, z} => h / @v _ @v
  Then: {ɕ, ɮ̃, ʎ̝̃} => {ȷ̊, ɥ̃, ɰ̃} / @v _ @v
  Then: nθ => n / @v _ @v
  Then: {d, d2, b, b2} => {ð, θ, β, bʱ} / @v _ @v
  Then: ɸ => β / @v _ @v

Late:
  @pk @c$1 => $1
  Then: @p @p$1 => $1
  Then: h @v$1 h => h $1
  Then: ə => * / @c _ @c
  Then: @vo => ə̥ / _ $
